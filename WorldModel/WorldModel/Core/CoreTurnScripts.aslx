<library>
  <function name="RunTurnScripts">
    if (IsGameRunning()) {
      if (game.menucallback = null) {
        foreach (turnscript, AllTurnScripts()) {
          if (GetBoolean(turnscript, "enabled")) {
            inscope = false
            if (turnscript.parent = game or turnscript.parent = null) {
              inscope = true
            } else {
              if (Contains(turnscript.parent, game.pov)) {
                inscope = true
              }
            }
            if (inscope) {
              do (turnscript, "script")
            }
          }
        }
      }
    }
  </function>
  
  <function name="EnableTurnScript" parameters="turnscript">
    turnscript.enabled = true
  </function>

  <function name="DisableTurnScript" parameters="turnscript">
    turnscript.enabled = false
  </function>

  <function name="SetTurnScript" parameters="turnscript, script">
    turnscript.script = script
  </function>

  <function name="SetTurnTimeout" parameters="turncount, script">
    SetTurnTimeoutID (turncount, "", script)
  </function>

  <function name="SetTurnTimeoutID" parameters="turncount, name, script">
    turnscriptname = ""
    if (name = "") {
      turnscriptname = GetUniqueElementName("turnscript")
    } else {
      if (GetObject(name) = null and GetTimer(name) = null) {
        turnscriptname = name
      } else {
        error ("Error creating turnscript: There is already an existing object named " + name)
      }
    }
    if (not turnscriptname=""){
      create turnscript (turnscriptname)
      turnscript = GetObject(turnscriptname)
      turnscript.turncount = 0
      turnscript.triggerturncount = turncount
      turnscript.timeoutscript = script
      SetTurnScript(turnscript) {
        this.turncount = this.turncount + 1
        if (this.turncount >= this.triggerturncount) {
        this.enabled = false
        invoke (this.timeoutscript)
        destroy (this.name)
        }
      }
      EnableTurnScript(turnscript)
    }
  </function>
</library>