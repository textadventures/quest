!library
!asl-version <350>
!name <QuestNet Standard Library>
!version <1.0>
!author <Alex Warren>
! This library adds useful additional QuestNet functions. We recommend you include this library in all QuestNet (multiplayer) games.

' NET.LIB v1.0
' for QuestNet Server 3.5
' Copyright © 2004 Axe Software. Please do not modify this library.

!QDK

	object <QuestNet>
		type <This object can be &given to other players; giveable>

!end


define type <giveable>
        action <give to anything> {
                if ( #quest.give.object.name# = player%userid% ) then {
                        msg <It is silly to give things to yourself!>
                }
                else {
                        if property <#quest.give.object.name#; netplayer> then {
                                move <$thisobject$; #quest.give.object.name#>
                                msg <You give $name(#quest.give.object.name#)$ the $thisobjectname$.>
                                msgto <#quest.give.object.name#; |b$name(%userid%)$|xb has given you a |b$thisobjectname$|xb.>
                                if action <$thisobject$; gain> then {
                                	with <#quest.give.object.name#> {
                                		doaction <$thisobject$; gain>
                                	}
                                }
                                if action <$thisobject$; lose> then {
                                	doaction <$thisobject$; lose>
                                }
                        }
                        else {
                                msg <The $displayname(#quest.give.object.name#)$ doesn't want that.>
                        }
                }
        }
end define


